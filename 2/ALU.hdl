// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    Inc16(in=true, out=zero);

    Mux16(a=x, b=zero, sel=zx, out=zcmpx);
    Mux16(a=y, b=zero, sel=zy, out=zcmpy);

    Not16(in=zcmpx, out=nottedx);
    Not16(in=zcmpy, out=nottedy);

    Mux16(a=zcmpx, b=nottedx, sel=nx, out=ncmpx);
    Mux16(a=zcmpy, b=nottedy, sel=ny, out=ncmpy);

    Add16(a=ncmpx, b=ncmpy, out=addxy);
    And16(a=ncmpx, b=ncmpy, out=andxy);

    Mux16(a=andxy, b=addxy, sel=f, out=addandxy);

    Not16(in=addandxy, out=naa);

    Mux16(a=addandxy, b=naa, sel=no, out=tmpout);

    And16(a=true, b=tmpout, out[15]=ng);

    And16(a=true, b=tmpout, out[0..7]=lbout);
    And16(a=true, b=tmpout, out[8..15]=hbout);

    Or8Way(in=lbout, out=lbz);
    Or8Way(in=hbout, out=hbz);

    Or(a=lbz, b=hbz, out=orb);
    Not(in=orb, out=zr);

    And16(a=true, b=tmpout, out=out);

    //// Replace this comment with your code.
}