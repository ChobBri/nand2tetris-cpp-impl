// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Math.jack
/**
 *  A library of commonly used mathematical functions.
 *  All functions runs in O(n), where n is the number of bits used
 *  for representing a two's complement integer value (16 in the Hack computer). 
 *  Note: Jack compilers implement multiplication and division
 *  using calls to OS functions in this class.
 */
class Math {
    static int n;             // Number of bits used for representing a two's complement integer
    static Array powersOfTwo; // Stores 2^0, 2^1, 2^2,..., 2^(n-1)

    // Initializes the Math library.
    function void init() {
        var int power, i;
        let n = 16;
        let power = 1;
        let i = 0;
        let powersOfTwo = Array.new(n);
        while (i < n)
        {
            let powersOfTwo[i] = power;
            let power = power + power;
            let i = i + 1;
        }
        return;
    }

    /** Returns the product of x and y. 
     *  When a Jack compiler detects the multiplication operator '*'
     *  in an expression, it handles it by invoking this method. 
     *  Thus, in Jack, x * y and Math.multiply(x,y) return the same value. */
    function int multiply(int x, int y) {
        var int sum, shiftedx;
        var int i;

        let sum = 0;
        let shiftedx = x;
        let i = 0;
        while (i < n)
        {
            if (Math.bit(y, i))
            {
                let sum = sum + shiftedx;
            }
            let shiftedx = shiftedx + shiftedx;
            let i = i + 1;
        }
        return sum;
    }

    /** Returns the integer part of x / y.
     *  When a Jack compiler detects the division operator '/'
     *  an an expression, it handles it by invoking this method.
     *  Thus, x/y and Math.divide(x,y) return the same value. */
    function int divide(int x, int y) {
        var int q, r;
        var int i;
        var int negCount;
        let i = n - 1;
        
        let negCount = 0;

        if (x < 0)
        {
            let x = -x;
            let negCount = negCount + 1;
        }

        if (y < 0)
        {
            let y = -y;
            let negCount = negCount + 1;
        }

        while (i > -1)
        {
            let r = r + r;
            if (Math.bit(x, i))
            {
                let r = r + 1;
            }
            if (r > (y - 1))
            {
                let r = r - y;
                let q = q + powersOfTwo[i];
            }
            let i = i - 1;
        }

        if (negCount = 1)
        {
            return -q;
        }

        return q;
    }

    /** Returns the integer part of the square root of x. */
    function int sqrt(int x) {
        var int y;
        var int i;
        var int power, power2;

        let y = 0;
        let i = (n / 2) - 1;
        while (i > -1)
        {
            let power = y + powersOfTwo[i];
            let power2 = power * power;
            if (((power2 - 1) < x) & (power2 > 0))
            {
                let y = power;
            }
            let i = i - 1;
        }
        return y;
    }

    /** Returns the greater value. */
    function int max(int a, int b) {
        if (a > b)
        {
            return a;
        }
        return b;
    }

    /** Returns the smaller value. */
    function int min(int a, int b) {
        if (a < b)
        {
            return a;
        }
        return b;
    }

    /** Returns the absolute value of x. */
    function int abs(int x) {
        if (x < 0)
        {
            return -x;
        }
        return x;
    }

    function boolean bit(int x, int i)
    {
        return ~((x & powersOfTwo[i]) = 0);
    }

    function int pow2(int x)
    {
        return powersOfTwo[x];
    }
}
